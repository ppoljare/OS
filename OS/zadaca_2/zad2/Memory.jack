/**
 * Memory operations library.
 */ 
class Memory {
    static Array ram;
    static Array freeList;
    
    /** Initializes memory parameters. */
    function void init() {
        let ram = 0;
        
        // set heap and free list to 2048 (heap top)
        let freeList = 2048;
        
        // initial values
        let freeList[0] = 0;     // no next block at start
        let freeList[1] = 14334; // initial block length, 16384 - 2048 - 2
        
        return;
    }

    /** Returns the value of the main memory at the given address. */
    function int peek(int address) {
        return ram[address];
    }

    /** Sets the value of the main memory at this address
     *  to the given value. */
    function void poke(int address, int value) {
        let ram[address] = value;
        return;
    }

    /** finds and allocates from the heap a memory block of the 
     *  specified size and returns a reference to its base address. */
    function int _alloc(int size) {
        var int curr, next, len, new;
        let curr = freeList;
        
        // iterate through all blocks
        while (~(curr = 0)) {
            let next = ram[curr];
            let len = ram[curr + 1];
            
            // check if current block is large enough
            if (~(len < size + 2)) { // len >= size + 2
                // the block is large enough, use it (first fit) 
                
                let ram[curr + 1] = len - size - 2;
                
                let new = curr + len - size;
                let ram[new] = 0;
                let ram[new + 1] = size;
                
                // return first element
                return new + 2;
            }            
            
            let curr = next;
        }
        return freeList;
    }
    
    function int alloc(int size) {
        var int res;
        
        let res = Memory._alloc(size);
        if (res = freeList) {
            do Memory.defrag();
            return Memory._alloc(size);
        }
        
        return res;
    }

    /** De-allocates the given object and frees its space. */
    function void deAlloc(int object) {
        var int curr, next;
        
        let curr = freeList;
        let next = ram[curr];
        while (~(next=0)) {
            if (object < next) {
                let ram[curr] = object-2;
                let ram[object-2] = next;
                return;
            }
            
            let curr = next;
            let next = ram[curr];
        }
        
        let ram[curr] = object-2;
        return;
    }
    
    /* Copies the values of d bytes from src to dst. */
    function int memcpy(int src, int dst, int d) {
        var int i;
        
        let i = 0;
        while (i < d) {
            let ram[dst+i] = ram[src+i];
            let i = i + 1;
        }
        
        return dst;
    }
    
    /* Defragments the memory. */
    function void defrag() {
        var int curr, next;
        
        let curr = freeList;
        let next = ram[curr];
        while (~(next=0)) {
            if (next = (curr + ram[curr+1] + 2)) {
                let ram[curr] = ram[next];
                let ram[curr+1] = ram[curr+1] + ram[next+1] + 2;
            } else {
                let curr = next;
            }
            
            let next = ram[curr];
        }
        
        return;
    }
}
